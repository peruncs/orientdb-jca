group = 'com.peruncs'
version = "${orientdb_jca_version}"

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = "${java_lang_level}"
targetCompatibility = "${java_lang_level}"

tasks.withType(JavaCompile) {
    options.incremental = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xlint:deprecation"
}

buildscript {

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'http://oss.sonatype.org/content/repositories/snapshots/' }
        mavenLocal()
    }

}

repositories {

    mavenCentral()
    jcenter()
    maven { url 'http://oss.sonatype.org/content/repositories/snapshots/' }
    mavenLocal()

}

dependencies {
    testCompile "junit:junit:${junit_version}"
    compileOnly "org.slf4j:slf4j-api:${slf4j_version}"
    compileOnly "javax:javaee-api:${java_ee_version}"
    compileOnly "com.orientechnologies:orientdb-server:${orientdb_version}"
    compileOnly "com.orientechnologies:orientdb-distributed:${orientdb_version}"
    compileOnly "com.orientechnologies:orientdb-graphdb:${orientdb_version}"
    compileOnly "com.orientechnologies:orientdb-client:${orientdb_version}"
}



task apiJar(type: Jar, dependsOn: classes) {
    classifier 'api'
    from sourceSets.main.output
    include('com/peruncs/odb/api/**')
}

task implJar(type: Jar, dependsOn: classes) {
    classifier 'impl'
    from sourceSets.main.output
    include('com/peruncs/odb/impl/**')
}

task srcJar(type: Jar, dependsOn: apiJar) {
    classifier = 'src'
    from sourceSets.main.allSource
}



publishing {

    publications {
        mavenPublication(MavenPublication) {
            //from components.java
            artifact implJar
            artifact apiJar
            artifact srcJar

            pom.withXml {
                asNode().with {
                    appendNode('description', 'OrientDB JCA adapter for Java EE')
                    appendNode('name', 'OrientDB JCA')
                    appendNode('url', 'https://github.com/peruncs/orientdb-jca')
                    appendNode('organization').with {
                        appendNode('name', 'Perun Consulting Services, Inc')
                        appendNode('url', 'www.peruncs.com')
                    }
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', 'Apache 2.0')
                            appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0')
                        }
                    }
                }
            }
        }
    }
}